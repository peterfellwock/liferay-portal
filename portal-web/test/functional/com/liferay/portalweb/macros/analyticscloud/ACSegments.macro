definition {

	macro accessSegment {
		var key_segmentName = "${segmentName}";

		Click(locator1 = "ACSegments#SELECT_SEGMENT");
	}

	macro addNestedSegmentField {
		Type(
			locator1 = "ACSegments#SEARCH_SEGMENT",
			value1 = "${segmentField}");

		var key_SegmentField = "${segmentField}";

		if (!(isSet(index))) {
			var index = "1";
		}

		DragAndDrop.javaScriptDragAndDropToObject(
			index = "${index}",
			locator1 = "ACSegments#SEGMENT_CRITERION_ITEM",
			locator2 = "ACSegments#CRITERIA_STATEMENT_BODY");
	}

	macro addSegmentField {
		Type(
			locator1 = "ACSegments#SEARCH_SEGMENT",
			value1 = "${segmentField}");

		var key_SegmentField = "${segmentField}";

		DragAndDrop.javaScriptDragAndDropToObject(
			locator1 = "ACSegments#SEGMENT_CRITERION_ITEM",
			locator2 = "ACSegments#SEGMENT_DROP_AREA");
	}

	macro addStaticMember {
		var key_TableMember = "${tableMember}";

		Click(locator1 = "ACSegments#ADD_MEMBERS_BUTTON");

		Click(locator1 = "ACSegments#TABLE_CHECKBOX");

		Click(locator1 = "ACSegments#ADD_BUTTON");
	}

	macro assertSegmentPresent {
		var key_segmentName = "${segmentName}";

		AssertElementPresent(locator1 = "ACSegments#SELECT_SEGMENT");
	}

	macro cancelSegment {
		Click(locator1 = "ACSegments#CANCEL_SEGMENT");
	}

	macro createDynamicSegment {
		Click(locator1 = "ACSegments#CREATE_SEGMENT_BUTTON");

		ACSegments.selectSegmentType(segmentType = "DYNAMIC");
	}

	macro createStaticSegment {
		Click(locator1 = "ACSegments#CREATE_SEGMENT_BUTTON");

		ACSegments.selectSegmentType(segmentType = "STATIC");
	}

	macro deleteSegment {
		ACSegments.searchSegment(findSegment = "${searchTerm}");

		Click(locator1 = "ACSegments#SEGMENT_ELLIPSIS");

		Click(locator1 = "ACSegments#DELETE_SEGMENT");

		Click(locator1 = "ACSegments#CONFIRM_DELETION");
	}

	macro deleteSegmentCriteria {
		if (!(isSet(index))) {
			var index = "1";
		}

		Click(
			criteriaName = "${criteriaName}",
			index = "${index}",
			locator1 = "ACSegments#CRITERIA_ELLIPSIS");

		MenuItem.click(menuItem = "Delete");
	}

	macro deleteSegmentViaSegmentListInlineButton {
		var key_segmentName = "${segmentName}";

		Click.javaScriptClick(locator1 = "ACSegments#DELETE_SEGMENT_INLINE_BUTTON");

		Click(locator1 = "ACSegments#CONFIRM_DELETION");
	}

	macro duplicateSegmentCriteria {
		if (!(isSet(index))) {
			var index = "1";
		}

		Click(
			criteriaName = "${criteriaName}",
			index = "${index}",
			locator1 = "ACSegments#CRITERIA_ELLIPSIS");

		MenuItem.click(menuItem = "Duplicate");
	}

	macro editConjunction {
		if (!(isSet(index))) {
			var index = "1";
		}

		Click(
			index = "${index}",
			locator1 = "ACSegments#CONJUNCTION_BUTTON");
	}

	macro editDateCriterion {
		if (IsElementNotPresent(locator1 = "ACSegments#DATE_PICKER")) {
			Click(
				key_text = "calendar",
				locator1 = "Icon#ANY");
		}

		Select(
			key_indexCalendar = "2",
			locator1 = "ACSegments#MONTH_AND_YEAR_CALENDAR",
			value1 = "${year}");

		Select(
			key_indexCalendar = "1",
			locator1 = "ACSegments#MONTH_AND_YEAR_CALENDAR",
			value1 = "${month}");

		Click(
			key_day = "${day}",
			locator1 = "ACSegments#DAY_CALENDAR_OPTION");
	}

	macro editFormControlCriterion {
		var key_Criterion = "${criterionName}";

		var key_option = "${option}";

		Click(locator1 = "ACSegments#FORM_CONTROL_CRITERION");

		Click(locator1 = "ACSegments#FORM_CONTROL_CRITERION_OPTION");
	}

	macro editNumberCriterion {
		if (!(isSet(index))) {
			var index = "1";
		}

		Type(
			index = "${index}",
			locator1 = "ACSegments#NUMBER_INPUT",
			value1 = "${numberInput}");

		Click(locator1 = "ACSegments#TOTAL_MEMBERS");
	}

	macro editSegment {
		if (!IsTextEqual.isPartialText(locator1 = "ACSegments#SEGMENT_LABEL", value1 = "SEGMENT")) {
			ACSegments.searchSegment(findSegment = "${searchTerm}");

			Click(locator1 = "ACSegments#SEGMENT_ELLIPSIS");
		}

		Click(locator1 = "ACSegments#EDIT_SEGMENT");
	}

	macro editSessionCriterion {
		echo("TODO");
	}

	macro editTextCriterion {
		if (!(isSet(index))) {
			var index = "1";
		}

		Type(
			index = "${index}",
			locator1 = "ACSegments#TEXT_INPUT",
			value1 = "${textInput}");

		Click(locator1 = "ACSegments#TOTAL_MEMBERS");
	}

	macro editWebBehaviorCriterion {
		if (isSet(activitiesOption)) {
			Select(
				key_indexField = "${indexField}",
				locator1 = "ACSegments#ACTIVITIES_CRITERION",
				value1 = "${activitiesOption}");
		}

		if (isSet(searchTerm)) {
			ACSegments.selectDocument(
				documentTitle = "${searchTerm}",
				indexField = "${indexField}");
		}

		if (isSet(atLeastMost)) {
			Select(
				key_indexField = "${indexField}",
				locator1 = "ACSegments#OCCURENCE_SELECT",
				value1 = "${atLeastMost}");
		}

		if (isSet(occurenceNumber)) {
			ACSegments.setOccurence(occurenceNumber = "${occurenceNumber}");
		}

		if (isSet(timeOption) || isSet(timePeriod)) {
			ACSegments.editWebBehaviorTimeCriterion(
				day = "${day}",
				day2 = "${day2}",
				indexField = "${indexField}",
				month = "${month}",
				month2 = "${month2}",
				timeOption = "${timeOption}",
				timePeriod = "${timePeriod}",
				year = "${year}",
				year2 = "${year2}");
		}
	}

	macro editWebBehaviorTimeCriterion {
		if (isSet(timeOption)) {
			Select(
				key_indexField = "${indexField}",
				locator1 = "ACSegments#TIME_INPUT_CRITERION_CONJUNCTION",
				value1 = "${timeOption}");
		}

		if (isSet(timePeriod)) {
			if ("${timeOption}" == "since" || !(isSet(timeOption))) {
				Select(
					key_indexField = "${indexField}",
					locator1 = "ACSegments#TIME_INPUT_CRITERION",
					value1 = "${timePeriod}");
			}
		}

		if ("${timeOption}" == "after" || "${timeOption}" == "before" || "${timeOption}" == "on") {
			ACSegments.editDateCriterion(
				day = "${day}",
				month = "${month}",
				year = "${year}");
		}

		if ("${timeOption}" == "between") {
			ACSegments.editDateCriterion(
				day = "${day}",
				month = "${month}",
				year = "${year}");

			ACSegments.editDateCriterion(
				day = "${day2}",
				month = "${month2}",
				year = "${year2}");
		}
	}

	macro filterAndOrderForSegLists {
		var key_filterName = "${filterName}";

		Click(locator1 = "ACSegments#FILTER_AND_ORDER_BUTTON");

		Click(
			locator1 = "ACSegments#FILTER_DROPDOWN_OPTION",
			value1 = "${key_filterName}");

		Click(locator1 = "ACSegments#FILTER_AND_ORDER_BUTTON");
	}

	macro goToSidebarAttributes {
		var key_Criterion = "${criterion}";

		Click(locator1 = "ACSegments#CRITERION_TYPE_DROPDOWN");

		Click(
			locator1 = "ACSegments#SELECT_CRITERION_TYPE",
			value1 = "${criterion}");
	}

	macro includeAnonymous {
		Check.checkToggleSwitch(locator1 = "ACSegments#ANONYMOUS_SWITCH");
	}

	macro nameSegment {
		WaitForElementPresent(locator1 = "ACSegments#SEGMENT_CREATION_PAGE");

		if (IsTextEqual.isPartialText(locator1 = "ACSegments#SEGMENT_LABEL", value1 = "DYNAMIC")) {
			Click(locator1 = "ACSegments#EDIT_NAME_BUTTON");
		}

		Type.sendKeys(
			locator1 = "ACSegments#NAME_FIELD",
			value1 = "${segmentName}");

		Click(locator1 = "ACSegments#SEGMENT_LABEL");
	}

	macro previewSegmentMembership {
		WaitForElementNotPresent(locator1 = "ACSegments#FORM_HEADER_LOADING_ANIMATION");

		Click(locator1 = "Icon#EYE_PREVIEW");
	}

	macro saveSegment {
		Click(locator1 = "ACSegments#SAVE_SEGMENT");

		Alert.viewSuccessMessage();
	}

	macro searchSegment {
		var searchTerm = "${findSegment}";

		Type(
			locator1 = "ACSegments#SEARCH_SEGMENT",
			value1 = "${searchTerm}");

		KeyPress(
			locator1 = "ACSettings#SEARCH_BAR",
			value1 = "\ENTER");
	}

	macro selectDocument {
		var key_indexField = "${indexField}";

		var key_TableMember = "${documentTitle}";

		Click(locator1 = "ACSegments#SELECT_BUTTON");

		ACSegments.searchSegment(findSegment = "${documentTitle}");

		Click(locator1 = "ACSegments#TABLE_CHECKBOX");

		Click(locator1 = "ACSegments#ADD_BUTTON");
	}

	macro selectOperator {
		if (!(isSet(index))) {
			var index = "1";
		}

		Select(
			key_indexField = "${index}",
			locator1 = "ACSegments#ACTIVITIES_CRITERION",
			value1 = "${operator}");
	}

	macro selectSegmentType {
		var key_SegmentType = "${segmentType}";

		Click(locator1 = "ACSegments#SELECT_SEGMENT_TYPE");
	}

	macro setOccurence {
		Type(
			locator1 = "ACSegments#OCCURENCE_INPUT",
			value1 = "${occurenceNumber}");
	}

	macro viewActivePageCreationModel {
		AssertTextEquals(
			locator1 = "ACSegments#ACTIVE_PAGE",
			value1 = "${pageNumber}");
	}

	macro viewConjunction {
		if (!(isSet(index))) {
			var index = "1";
		}

		AssertTextEquals(
			index = "${index}",
			locator1 = "ACSegments#CONJUNCTION_BUTTON",
			value1 = "${conjunction}");
	}

	macro viewDeletedCriteria {
		AssertTextPresent(
			locator1 = "ACSegments#DELETED_CRITERIA",
			value1 = "Drag and drop criterion from the right to add rules.");
	}

	macro viewDeletedSegmentName {
		var key_segmentName = "${searchTerm}";

		ACSegments.searchSegment(findSegment = "${searchTerm}");

		AssertElementNotPresent(locator1 = "ACSegments#SELECT_SEGMENT");
	}

	macro viewDuplicatedCriteria {
		var i = "0";

		while ("${i}" != "${totalCriteriaCount}") {
			var i = ${i} + 1;

			AssertVisible(
				criteriaName = "${criteriaName}",
				index = "${i}",
				locator1 = "ACSegments#DUPLICATED_CRITERIA");
		}
	}

	macro viewEditorLabel {
		AssertTextPresent(
			locator1 = "ACSegments#EDITOR_LABEL",
			value1 = "${labelName}");
	}

	macro viewIncludeAnonymousLabel {
		AssertElementPresent(locator1 = "ACSegments#ANONYMOUS_LABEL");
	}

	macro viewSegmentComposition {
		if ("${individualsType}" == "Active") {
			AssertTextEquals(
				locator1 = "ACSegments#SEGMENT_COMPOSITION_COUNT",
				value1 = "${individualsNumber}");
		}
		else if ("${individualsType}" == "Known") {
			AssertTextEquals(
				locator1 = "ACSegments#SEGMENT_COMPOSITION_COUNT",
				value1 = "${individualsNumber}");
		}
	}

	macro viewSegmentCriteriaIndividualAttributes {
		if (!(isSet(index))) {
			var index = "1";
		}

		AssertVisible(
			index = "${index}",
			locator1 = "ACSegments#SEGMENT_CRITERIA_TYPE",
			type = "Individual");

		AssertTextEquals(
			index = "${index}",
			locator1 = "ACSegments#SEGMENT_CRITERIA_ATTRIBUTE_NAME",
			value1 = "${attributeName}");

		AssertTextEquals(
			index = "${index}",
			locator1 = "ACSegments#SEGMENT_CRITERIA_CONDITION",
			value1 = "${condition}");

		AssertTextEquals(
			index = "${index}",
			locator1 = "ACSegments#SEGMENT_CRITERIA_ATTRIBUTE_VALUE",
			value1 = "${attributeValue}");

		if (isSet(conjunction)) {
			AssertTextEquals(
				index = "${index}",
				locator1 = "ACSegments#SEGMENT_CRITERIA_CONJUNCTION",
				value1 = "${conjunction}");
		}
	}

	macro viewSegmentCriteriaWebBehavior {
		if (isSet(activitiesOption)) {
			AssertTextEquals(
				key_index = "1",
				locator1 = "ACSegments#SEGMENT_CRITERIA_WEB_BEHAVIOR_SPAN",
				value1 = "${activitiesOption}");
		}

		if (isSet(searchTerm)) {
			AssertTextEquals(
				key_index = "1",
				locator1 = "ACSegments#SEGMENT_CRITERIA_WEB_BEHAVIOR_B",
				value1 = "${searchTerm}");
		}

		if (isSet(atLeastMost)) {
			AssertTextEquals(
				key_index = "3",
				locator1 = "ACSegments#SEGMENT_CRITERIA_WEB_BEHAVIOR_SPAN",
				value1 = "${atLeastMost}");
		}

		if (isSet(occurenceNumber)) {
			AssertTextEquals(
				key_index = "2",
				locator1 = "ACSegments#SEGMENT_CRITERIA_WEB_BEHAVIOR_B",
				value1 = "${occurenceNumber}");
		}

		if (isSet(timeOption)) {
			AssertTextEquals(
				key_index = "5",
				locator1 = "ACSegments#SEGMENT_CRITERIA_WEB_BEHAVIOR_SPAN",
				value1 = "${timeOption}");
		}

		if (isSet(timePeriod)) {
			AssertTextEquals(
				key_index = "6",
				locator1 = "ACSegments#SEGMENT_CRITERIA_WEB_BEHAVIOR_SPAN",
				value1 = "${timePeriod}");
		}

		if (isSet(date)) {
			AssertTextEquals(
				key_index = "6",
				locator1 = "ACSegments#SEGMENT_CRITERIA_WEB_BEHAVIOR_SPAN",
				value1 = "${date}");
		}
	}

	macro viewSegmentMembershipLegendNumbers {
		AssertTextEquals(
			key_memberType = "Known Members:",
			locator1 = "ACSegments#SEGMENT_MEMBERSHIP_LEGEND",
			value1 = "${knownNumber}");

		AssertTextEquals(
			key_memberType = "Anonymous Members:",
			locator1 = "ACSegments#SEGMENT_MEMBERSHIP_LEGEND",
			value1 = "${anonymousNumber}");

		AssertTextEquals(
			key_memberType = "Total Members:",
			locator1 = "ACSegments#SEGMENT_MEMBERSHIP_LEGEND",
			value1 = "${totalNumber}");
	}

	macro viewSegmentName {
		var key_segmentName = "${searchTerm}";

		ACSegments.searchSegment(findSegment = "${searchTerm}");

		AssertElementPresent(locator1 = "ACSegments#SELECT_SEGMENT");
	}

	macro viewSegmentProfileCards {
		AssertTextPresent(
			locator1 = "ACSegments#SEGMENT_MEMBERSHIP_CARD",
			value1 = "Segment Membership");

		AssertTextPresent(
			locator1 = "ACSegments#SEGMENT_TOP_INTERESTS_CARD",
			value1 = "Top Interests");

		AssertTextPresent(
			locator1 = "ACSegments#SEGMENT_BREAKDOWN_CARD",
			value1 = "Add a Breakdown by Attribute");
	}

	macro viewSegmentSpecificOrder {
		var resultOrder = "0";

		for (var segments : list "${segments}") {
			var resultOrder = ${resultOrder} + 1;

			AssertVisible(
				key_segmentName = "${segments}",
				locator1 = "ACSegments#VIEW_SPECIFIC_SEGMENT_TABLE",
				resultOrder = "${resultOrder}");
		}
	}

}