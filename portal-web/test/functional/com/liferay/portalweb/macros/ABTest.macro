definition {

	macro checkAnalyticsCloudInfo {
		if (IsElementPresent(locator1 = "ABTest#REVIEW_AND_RUN_TEST_BUTTON")) {
			ABTest.runTest();
		}

		Click(locator1 = "ABTest#VIEW_DATA_ANALYTICS_CLOUD_BUTTON");

		SelectWindow(locator1 = "name=undefined");

		if (IsElementPresent(locator1 = "AnalyticsCloudConnection#ANALYTICS_CLOUD_USER_LOGIN_FIELD")) {
			AnalyticsCloud.loginAnalyticsCloud(
				emailAddress = "test@liferay.com",
				password = "test");
		}

		AssertTextEquals(
			ABtestTitle = "${ABtestTitle}",
			locator1 = "ABTest#AB_TEST_TITLE_AC_SIDE",
			value1 = "${ABtestTitle}");

		Close(locator1 = "title=Tests - Analytics Cloud");
	}

	macro createABTest {
		while (IsElementNotPresent(locator1 = "ABTest#CREATE_TEST_BUTTON")) {
			Click(locator1 = "ABTest#AB_TEST_BUTTON");
		}

		Click(locator1 = "ABTest#CREATE_TEST_BUTTON");

		Click(locator1 = "ABTest#AB_TEST_TITLE_FIELD");

		Type(
			locator1 = "ABTest#AB_TEST_TITLE_FIELD",
			value1 = "${testTitle}");

		AssertClick(
			locator1 = "ABTest#SELECT_GOAL",
			value1 = "${goal}");

		Click(locator1 = "ABTest#SAVE_AB_TEST_BUTTON");
	}

	macro createVariant {
		Click(locator1 = "ABTest#CREATE_VARIANT_BUTTON");

		Click(locator1 = "ABTest#VARIANT_NAME_FIELD");

		Type(
			locator1 = "ABTest#VARIANT_NAME_FIELD",
			value1 = "${variantName}");

		Click(locator1 = "ABTest#SAVE_VARIANT_NAME_BUTTON");
	}

	macro deleteVariant {
		Click(locator1 = "ABTest#VARIANT_DROPDOWN");

		ClickNoError(locator1 = "ABTest#DELETE_VARIANT_BUTTON");

		AssertConfirm.assertConfirmationNoError(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();
	}

	macro runTest {
		Click(locator1 = "ABTest#REVIEW_AND_RUN_TEST_BUTTON");

		if (IsElementPresent(locator1 = "ABTest#RUN_TEST_BUTTON")) {
			ClickNoError(locator1 = "ABTest#RUN_TEST_BUTTON");

			Click(locator1 = "ABTest#OK_BUTTON");
		}
	}

	macro selectElementID {
		Click(locator1 = "ABTest#SELECT_CLICKABLE_ELEMENT_BUTTON");

		Click(locator1 = "ABTest#SELECT_CLICKABLE_ELEMENT");

		Click(locator1 = "ABTest#SET_ELEMENT_AS_CLICK_TARGET_BUTTON");

		Alert.viewSuccessMessage();

		AssertTextEquals(
			locator1 = "ABTest#TARGET_LABEL",
			value1 = "Target");
	}

	macro terminateTest {
		if (IsElementPresent(locator1 = "ABTest#TERMINATE_TEST_BUTTON")) {
			ClickNoError(locator1 = "ABTest#TERMINATE_TEST_BUTTON");
		}
		else {
			Refresh();

			ClickNoError(locator1 = "ABTest#TERMINATE_TEST_BUTTON");
		}

		AssertConfirm.assertConfirmationNoError(value1 = "Are you sure you want to terminate this test?");

		Alert.viewSuccessMessage();
	}

	macro writeId {
		Click(locator1 = "ABTest#ELEMENT_ID_INPUT");

		Type(
			locator1 = "ABTest#ELEMENT_ID_INPUT",
			value1 = "${elementId}");
	}

}